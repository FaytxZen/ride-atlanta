apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'jacoco'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion 26
    buildToolsVersion "25.0.3"
    defaultConfig {
        applicationId "com.andrewvora.apps.rideatlanta"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 2
        versionName "1.1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false

            buildConfigField "String", "MARTA_API_KEY", "\"$System.env.RIDEATLANTA_MARTA_API_KEY\""
            buildConfigField "String", "FABRIC_API_KEY", "\"$System.env.RIDEATLANTA_FABRIC_API_KEY\""
            buildConfigField "String", "TWITTER_API_KEY", "\"$System.env.RIDEATLANTA_TWITTER_API_KEY\""
            buildConfigField "String", "TWITTER_SECRET", "\"$System.env.RIDEATLANTA_TWITTER_SECRET\""
        }
        release {
            minifyEnabled true
            shrinkResources true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            buildConfigField "String", "MARTA_API_KEY", "\"$System.env.RIDEATLANTA_PROD_MARTA_API_KEY\""
            buildConfigField "String", "FABRIC_API_KEY", "\"$System.env.RIDEATLANTA_PROD_FABRIC_API_KEY\""
            buildConfigField "String", "TWITTER_API_KEY", "\"$System.env.RIDEATLANTA_PROD_TWITTER_API_KEY\""
            buildConfigField "String", "TWITTER_SECRET", "\"$System.env.RIDEATLANTA_PROD_TWITTER_SECRET\""
        }
    }

    testOptions {
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    def app_compat_version = '26.0.0'
    def dagger_version = '2.11'
    def butter_knife_version = '8.4.0'
    def retrofit_version = '2.3.0'

    // System Dependencies
    compile "com.android.support:appcompat-v7:$app_compat_version"
    compile "com.android.support:design:$app_compat_version"
    compile "com.android.support:cardview-v7:$app_compat_version"
    compile "com.android.support:recyclerview-v7:$app_compat_version"
    compile('com.twitter.sdk.android:twitter:2.2.0@aar') {
        transitive = true;
    }

    compile 'io.reactivex.rxjava2:rxjava:2.1.0'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'

    compile "com.jakewharton:butterknife:$butter_knife_version"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butter_knife_version"

    compile "com.google.dagger:dagger:$dagger_version"
    compile "com.google.dagger:dagger-android:$dagger_version"
    compile "com.google.dagger:dagger-android-support:$dagger_version"
    compile "com.google.code.findbugs:jsr305:3.0.2"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-android-processor:$dagger_version"
    compile "com.squareup.retrofit2:retrofit:$retrofit_version"
    compile "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    compile 'com.android.volley:volley:1.0.0'
    compile 'com.google.code.gson:gson:2.7'

    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    })

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
}

jacoco {
    toolVersion '0.7.9'
    reportsDir = file("$buildDir/jacocoReports")
}

task jacocoTestReport(type: JacocoReport, dependsOn: ['test']) {

    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*']
    def debugTree = fileTree(dir: "${buildDir}/intermediates/classes/debug", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java"

    sourceDirectories = files([mainSrc])
    classDirectories = files([debugTree])
    executionData = fileTree(dir: "$buildDir", includes: [
            "jacoco/testDebugUnitTest.exec"
    ])
}
